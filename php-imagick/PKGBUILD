# Contributor: Spencer Rinehart <anubis@overthemonkey.com>
# from AUR
# no changes, except
# * commented out unnecessary install script
# * sha512 checksum

releasepage=('https://aur.archlinux.org/packages/php-imagick/')
pkgverforked=('3.4.3-5')

pkgname=php-imagick
_extname=imagick
pkgver=3.4.3
pkgrel=6
pkgdesc="PHP extension for IMagick"
arch=('i686' 'x86_64' 'armv7h')
url="https://pecl.php.net/package/${_extname}"
license=('PHP')
depends=('php>=5.4.0' 'imagemagick>=6.5.3')
backup=("etc/php/conf.d/${_extname}.ini")
# install="php-${_extname}.install"
source=("https://pecl.php.net/get/${_extname}-${pkgver}.tgz")
sha512sums=('2cb2b856cf83a78d3542cdf7c69554dcc063a0541e9092b24e5e1fbd8928066a4a3de154049d0202c35addbc5055ccfbb5910ae92f2f13da80ddfc025340ddcd')

build() {
  cd "${_extname}-${pkgver}"

  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "${_extname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  echo "extension=${_extname}.so" > "${_extname}.ini"
  install -D -m644 "${_extname}.ini" "${pkgdir}/etc/php/conf.d/${_extname}.ini"
}

