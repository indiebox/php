# Maintainer: Fran√ßois Freitag <mail@franek.fr>
# Modified for UBOS: Johannes Ernst <jernst@indiecomputing.com>
# * add releasepage
# * added pkgverforked
# * do not install .ini file so UBOS only activates it if needed
pkgname=php-igbinary
_pkgname=igbinary
pkgver=3.0.1
pkgrel=1
pkgdesc="Igbinary is a drop in replacement for the standard php serializer."
arch=('any')
url='https://github.com/igbinary/igbinary'
license=('BSD')
depends=(php)
conflicts=(php-igbinary-git)
backup=("etc/php/conf.d/igbinary.ini")
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('4930670ecdc1796fa73184e8f12bf570f7b1a43cb422cede056ccd1ca28fc950b00a9418519c59a9c07e346a89153b6b105e873fcfccb1365d43d4abf98804da')
releasepage=('https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=php-igbinary')
pkgverforked=('3.0.1-1')

build() {
    cd "${_pkgname}-${pkgver}"

    phpize
    ./configure --enable-igbinary
    make
}

check() {
    cd "${_pkgname}-${pkgver}"

    NO_INTERACTION=1 make test
}

package() {
    cd "${_pkgname}-${pkgver}"

    make INSTALL_ROOT="$pkgdir" install
#   install -D -m0644 \
#       "${srcdir}/${_pkgname}-${pkgver}/igbinary.php.ini" \
#       "${pkgdir}/etc/php/conf.d/igbinary.ini"
    install -D -m0644 \
        "${srcdir}/${_pkgname}-${pkgver}/COPYING" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

